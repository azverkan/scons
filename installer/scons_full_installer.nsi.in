##########################################################################
# Global constants and definitions
##########################################################################
!define MULTIUSER_EXECUTIONLEVEL Highest
!define MULTIUSER_MUI
!define MULTIUSER_INSTALLMODE_COMMANDLINE
!define MULTIUSER_INSTALLMODE_INSTDIR "SCons"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY "@INSTALL_DIR_REG_KEY@"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME "InstallLocation"
!define MULTIUSER_INSTALLMODE_FUNCTION "SetRegHive"
!define MULTIUSER_INSTALLMODE_UNFUNCTION "un.SetRegHive"

!define MUI_ABORTWARNING

!define MUI_LANGDLL_REGISTRY_ROOT "HKCU" 
!define MUI_LANGDLL_REGISTRY_KEY "@INSTALL_DIR_REG_KEY@" 
!define MUI_LANGDLL_REGISTRY_VALUENAME "InstallerLanguage"

SetCompressor lzma

##########################################################################
# Additional headers
##########################################################################
!include MultiUser.nsh
!include MUI2.nsh
!include ..\..\installer\EnvVarUpdate.nsh

##########################################################################
# Installer pages
##########################################################################
Name "@INSTALLER_NAME@"

OutFile "@INSTALLER_FILE@"

Var StartMenuFolder

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE @LICENSE_DATA@
!insertmacro MULTIUSER_PAGE_INSTALLMODE
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "SCons"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT HKCU
!define MUI_STARTMENUPAGE_REGISTRY_KEY "@INSTALL_DIR_REG_KEY@"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "StartMenuFolder"
!insertmacro MUI_PAGE_STARTMENU "Application" $StartMenuFolder
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_LINK "SCons Website"
!define MUI_FINISHPAGE_LINK_LOCATION "http://www.scons.org"
!insertmacro MUI_PAGE_FINISH

##########################################################################
# Uninstaller pages
##########################################################################
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_COMPONENTS
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

##########################################################################
# L10n support
##########################################################################
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

!insertmacro MUI_RESERVEFILE_LANGDLL

##########################################################################
# Helper functions and macros
##########################################################################
Var RegHive

Function SetRegHive

  ${if} $MultiUser.InstallMode == "AllUsers"
    StrCpy $RegHive "HKLM"
  ${else}
    StrCpy $RegHive "HKCU"
  ${endif}

FunctionEnd

Function un.SetRegHive

  ${if} $MultiUser.InstallMode == "AllUsers"
    StrCpy $RegHive "HKLM"
  ${else}
    StrCpy $RegHive "HKCU"
  ${endif}

FunctionEnd

!macro MultiUserWriteRegStr subkey entry_name new_entry_string
  ${if} $MultiUser.InstallMode == "AllUsers"
    WriteRegStr HKLM "${subkey}" "${entry_name}" "${new_entry_string}"
  ${else}
    WriteRegStr HKCU "${subkey}" "${entry_name}" "${new_entry_string}"
  ${endif}
!macroend

!macro MultiUserDeleteRegKey subkey
  ${if} $MultiUser.InstallMode == "AllUsers"
    DeleteRegKey HKLM "${subkey}"
  ${else}
    DeleteRegKey HKCU "${subkey}"
  ${endif}
!macroend

##########################################################################
# Installation types
##########################################################################
InstType "Full"
InstType "HTML Documentation"
InstType "PDF Documentation"
InstType "Binary"

##########################################################################
# Installer sections
##########################################################################
LangString SEC_SconsExe ${LANG_ENGLISH} "SCons Executable"
LangString SEC_SconsExe ${LANG_GERMAN} "SCons (Programm)"

LangString SEC_AddToPath ${LANG_ENGLISH} "Add to path"
LangString SEC_AddToPath ${LANG_GERMAN} "Zum Pfad Hinzufügen"

LangString SEC_AddToContextMenu ${LANG_ENGLISH} "Add frontend to context menu"
LangString SEC_AddToContextMenu ${LANG_GERMAN} "Frontend zum Kontextmenü hinzufügen"

LangString SEC_HtmlDocs ${LANG_ENGLISH} "HTML Documentation"
LangString SEC_HtmlDocs ${LANG_GERMAN} "HTML-Dokumentation"

LangString SEC_HtmlUsersGuide ${LANG_ENGLISH} "User's Guide"
LangString SEC_HtmlUsersGuide ${LANG_GERMAN} "Benutzerhandbuch"

LangString SEC_HtmlManpages ${LANG_ENGLISH} "Man Pages"
LangString SEC_HtmlManpages ${LANG_GERMAN} "Manual-Seiten"

LangString SEC_HtmlApiReference ${LANG_ENGLISH} "API Reference"
LangString SEC_HtmlApiReference ${LANG_GERMAN} "API-Referenz"

LangString SEC_PdfDocs ${LANG_ENGLISH} "PDF Documentation"
LangString SEC_PdfDocs ${LANG_GERMAN} "PDF-Dokumentation"

LangString SEC_PdfUsersGuide ${LANG_ENGLISH} "User's Guide"
LangString SEC_PdfUsersGuide ${LANG_GERMAN} "Benutzerhandbuch"

LangString SEC_PdfApiReference ${LANG_ENGLISH} "API Reference"
LangString SEC_PdfApiReference ${LANG_GERMAN} "API-Referenz"

LangString SEC_LocalZip ${LANG_ENGLISH} "SCons source code"
LangString SEC_LocalZip ${LANG_GERMAN} "SCons-Quellcode"

LangString SEC_PythonModule ${LANG_ENGLISH} "Python modules"
LangString SEC_PythonModule ${LANG_GERMAN} "Python-Module"

Section
  SetOutPath $INSTDIR
  
  WriteUninstaller $INSTDIR\@UNINSTALLER_FILE@
  
  !insertmacro MultiUserWriteRegStr ${MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY} ${MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME} $INSTDIR
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" $INSTDIR\@UNINSTALLER_FILE@
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons Frontend.lnk" "$INSTDIR\scons-frontend.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
  
  # For details, see http://nsis.sourceforge.net/Add_uninstall_information_to_Add/Remove_Programs  
  !insertmacro MultiUserWriteRegStr "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PRODUCT_NAME@" "DisplayName" "@UNINSTALL_DISPLAY_NAME@"
  !insertmacro MultiUserWriteRegStr "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PRODUCT_NAME@" "UninstallString" "$\"$INSTDIR\@UNINSTALLER_FILE@$\""
  !insertmacro MultiUserWriteRegStr "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PRODUCT_NAME@" "QuietUninstallString" "$\"$INSTDIR\@UNINSTALLER_FILE@$\" /S"
SectionEnd

Section !$(SEC_SconsExe) SconsExe

  SectionIn 1
  SectionIn 4

  SetOutPath $INSTDIR

@SCONS_STANDALONE_FILES_INSTALL@

  File /r tcl
  File /r tk

SectionEnd

Section $(SEC_AddToPath) AddToPath

  SectionIn 1
  SectionIn 4

  ${EnvVarUpdate} $0 "PATH" "A" $RegHive $INSTDIR

SectionEnd

Section $(SEC_AddToContextMenu) AddToContextMenu

    SectionIn 1
    SectionIn 4
    
    !insertmacro MultiUserWriteRegStr "Software\Classes\*\shell\scons_frontend" "" "SCons Frontend"
    !insertmacro MultiUserWriteRegStr "Software\Classes\*\shell\scons_frontend\command" "" "$INSTDIR\scons-frontend.exe %1"
    !insertmacro MultiUserWriteRegStr "Software\Classes\Directory\shell\scons_frontend" "" "SCons Frontend"
    !insertmacro MultiUserWriteRegStr "Software\Classes\Directory\shell\scons_frontend\command" "" "$INSTDIR\scons-frontend.exe %1"
    
SectionEnd

SectionGroup /e $(SEC_HtmlDocs) HtmlDocs
  Section $(SEC_HtmlUsersGuide) HtmlUsersGuide
    SectionIn 1
    SectionIn 2
    
    SetOutPath $INSTDIR\doc

@HTML_USERS_GUIDE_FILES@

    !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons User's Guide (HTML) .lnk" $INSTDIR\doc\scons-user.html
    !insertmacro MUI_STARTMENU_WRITE_END

    SetOutPath $INSTDIR
  SectionEnd
  Section $(SEC_HtmlManPages) HtmlManpages
    SectionIn 1
    SectionIn 2
    
    SetOutPath $INSTDIR\doc

@HTML_MAN_PAGES_FILES@

    !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons Manual Page (HTML) .lnk" $INSTDIR\doc\scons-man.html
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons-Time Manual Page (HTML) .lnk" $INSTDIR\doc\scons-time-man.html
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SConsign Manual Page (HTML) .lnk" $INSTDIR\doc\sconsign-man.html
    !insertmacro MUI_STARTMENU_WRITE_END

    SetOutPath $INSTDIR
  SectionEnd
  Section $(SEC_HtmlApiReference) HtmlApiReference
    SectionIn 1
    SectionIn 2
    
    SetOutPath $INSTDIR\doc\api

@HTML_API_REFERENCE_FILES@

    !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons API Reference (HTML) .lnk" $INSTDIR\doc\api\index.html
    !insertmacro MUI_STARTMENU_WRITE_END


    SetOutPath $INSTDIR
  SectionEnd
SectionGroupEnd

SectionGroup /e $(SEC_PdfDocs) PdfDocs
  Section $(SEC_PdfUsersGuide) PdfUsersGuide
    SectionIn 1
    SectionIn 3
    
    SetOutPath $INSTDIR\doc

@PDF_USERS_GUIDE_FILES@

    !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons User's Guide (PDF) .lnk" $INSTDIR\doc\scons-user.pdf
    !insertmacro MUI_STARTMENU_WRITE_END

    SetOutPath $INSTDIR
  SectionEnd
  Section $(SEC_PdfApiReference) PdfApiReference
    SectionIn 1
    SectionIn 3
    
    SetOutPath $INSTDIR\doc

@PDF_API_REFERENCE_FILES@

    !insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
    CreateShortCut "$SMPROGRAMS\$StartMenuFolder\SCons API Reference (PDF) .lnk" $INSTDIR\doc\scons-api.pdf
    !insertmacro MUI_STARTMENU_WRITE_END


    SetOutPath $INSTDIR
  SectionEnd
SectionGroupEnd

Section $(SEC_LocalZip) LocalZip
  SectionIn 1

  @LOCAL_ZIP_FILES@
SectionEnd

Section $(SEC_PythonModule) PythonModule
  SectionIn 1
  
  @PYTHON_MODULES_FILES@

  BringToFront
SectionEnd

##########################################################################
# Installer l10n
##########################################################################
LangString DESC_HtmlDocs  ${LANG_ENGLISH} "SCons documentation in HTML format."
LangString DESC_HtmlDocs  ${LANG_GERMAN} "SCons-Dokumentation im HTML-Format."

LangString DESC_HtmlUsersGuide  ${LANG_ENGLISH} "SCons user's guide."
LangString DESC_HtmlUsersGuide  ${LANG_GERMAN} "SCons Benutzerhandbuch."

LangString DESC_HtmlManPages  ${LANG_ENGLISH} "Manual pages for scons (1), scons-time (1), and sconsign (1)."
LangString DESC_HtmlManPages  ${LANG_GERMAN} "Manual-Seiten für scons (1), scons-time (1) und sconsign (1)."

LangString DESC_HtmlApiReference  ${LANG_ENGLISH} "API reference for the SCons engine."
LangString DESC_HtmlApiReference  ${LANG_GERMAN} "API-Referenz für die SCons-Engine."


LangString DESC_PdfDocs  ${LANG_ENGLISH} "SCons documentation in PDF format."
LangString DESC_PdfDocs  ${LANG_GERMAN} "SCons-Dokumentation im PDF-Format."

LangString DESC_PdfUsersGuide  ${LANG_ENGLISH} "SCons user's guide."
LangString DESC_PdfUsersGuide  ${LANG_GERMAN} "SCons Benutzerhandbuch."

LangString DESC_PdfApiReference  ${LANG_ENGLISH} "API reference for the SCons engine."
LangString DESC_PdfApiReference  ${LANG_GERMAN} "API-Referenz für die SCons-Engine."

LangString DESC_SconsExe  ${LANG_ENGLISH} "The stand-alone SCons executable."
LangString DESC_SconsExe  ${LANG_GERMAN} "Eigenständig ausführbare Version von SCons."

LangString DESC_AddToPath ${LANG_ENGLISH} "Add the installation directory to the path."
LangString DESC_AddToPath ${LANG_GERMAN} "Fügt das Installationsverzeichnis zum Pfad hinzu."

LangString DESC_AddToContextMenu ${LANG_ENGLISH} "Add the frontend to directory and file context menus."
LangString DESC_AddToContextMenu ${LANG_GERMAN} "Fügt das Frontend zum Kontextmenü von Dateien und Ordnern hinzu."

LangString DESC_LocalZip ${LANG_ENGLISH} "SCons source code (zipped)."
LangString DESC_LocalZip ${LANG_GERMAN} "SCons-Quellcode (im Zip-Format)."

LangString DESC_PythonModule ${LANG_ENGLISH} "SCons scripts and engine for Python."
LangString DESC_PythonModule ${LANG_GERMAN} "SCons-Skripte und -Engine für Python."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${HtmlDocs}  $(DESC_HtmlDocs)
  !insertmacro MUI_DESCRIPTION_TEXT ${HtmlUsersGuide}  $(DESC_HtmlUsersGuide)
  !insertmacro MUI_DESCRIPTION_TEXT ${HtmlManPages}  $(DESC_HtmlManPages)
  !insertmacro MUI_DESCRIPTION_TEXT ${HtmlApiReference}  $(DESC_HtmlApiReference)
  !insertmacro MUI_DESCRIPTION_TEXT ${PdfDocs}  $(DESC_PdfDocs)
  !insertmacro MUI_DESCRIPTION_TEXT ${PdfUsersGuide}  $(DESC_PdfUsersGuide)
  !insertmacro MUI_DESCRIPTION_TEXT ${PdfApiReference}  $(DESC_PdfApiReference)
  !insertmacro MUI_DESCRIPTION_TEXT ${SconsExe}  $(DESC_SconsExe)
  !insertmacro MUI_DESCRIPTION_TEXT ${AddToPath} $(DESC_AddToPath)
  !insertmacro MUI_DESCRIPTION_TEXT ${AddToContextMenu} $(DESC_AddToContextMenu)
  !insertmacro MUI_DESCRIPTION_TEXT ${LocalZip} $(DESC_LocalZip)
  !insertmacro MUI_DESCRIPTION_TEXT ${PythonModule} $(DESC_PythonModule)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

##########################################################################
# Installer functions
##########################################################################
Function .onInit

  !insertmacro MUI_LANGDLL_DISPLAY
  !insertmacro MULTIUSER_INIT

# Taken from: http://nsis.sourceforge.net/Allow_only_one_installer_instance
  BringToFront
  ; Check if already running
  ; If so don't open another but bring to front
  System::Call "kernel32::CreateMutexA(i 0, i 0, t '$(^Name)') i .r0 ?e"
  Pop $0
  StrCmp $0 0 launch
   StrLen $0 "$(^Name)"
   IntOp $0 $0 + 1
  loop:
    FindWindow $1 '#32770' '' 0 $1
    IntCmp $1 0 +5
    System::Call "user32::GetWindowText(i r1, t .r2, i r0) i."
    StrCmp $2 "$(^Name)" 0 loop
    System::Call "user32::ShowWindow(i r1,i 9) i."         ; If minimized then maximize
    System::Call "user32::SetForegroundWindow(i r1) i."    ; Bring to front
    Abort
  launch:
# End taken from: http://nsis.sourceforge.net/Allow_only_one_installer_instance

FunctionEnd

Function .onSelChange
  SectionGetFlags ${SconsExe} $0
  IntOp $0 $0 & ${SF_SELECTED}
  ${if} $0 == 0
    SectionGetFlags ${AddToPath} $0
    IntOp $0 $0 ~
    IntOp $0 $0 | ${SF_SELECTED}
    IntOp $0 $0 ~
    IntOp $0 $0 | ${SF_RO}
    SectionSetFlags ${AddToPath} $0
  ${else}
    SectionGetFlags ${AddToPath} $0
    IntOp $0 $0 ~
    IntOp $0 $0 | ${SF_RO}
    IntOp $0 $0 ~
    SectionSetFlags ${AddToPath} $0
  ${endif}
FunctionEnd

##########################################################################
# Uninstaller sections
##########################################################################
LangString SEC_Uninstall ${LANG_ENGLISH} "Uninstall"
LangString SEC_Uninstall ${LANG_GERMAN} "Deinstallieren"

LangString SEC_UnRemoveFromPath ${LANG_ENGLISH} "Remove from path"
LangString SEC_UnRemoveFromPath ${LANG_GERMAN} "Aus dem Pfad entfernen"

LangString SEC_UnRemoveFromRegistry ${LANG_ENGLISH} "Remove from Registry"
LangString SEC_UnRemoveFromRegistry ${LANG_GERMAN} "Aus der Registry entfernen"

LangString SEC_UnRemovePythonModules ${LANG_ENGLISH} "Remove Python modules"
LangString SEC_UnRemovePythonModules ${LANG_GERMAN} "Python-Module entfernen"

Section "un.$(SEC_Uninstall)" Uninstall

  Delete $INSTDIR\@UNINSTALLER_FILE@

@HTML_USERS_GUIDE_FILES_UNINSTALL@
@HTML_MAN_PAGES_FILES_UNINSTALL@
@HTML_API_REFERENCE_FILES_UNINSTALL@

@PDF_USERS_GUIDE_FILES_UNINSTALL@
@PDF_API_REFERENCE_FILES_UNINSTALL@

@SCONS_STANDALONE_FILES_UNINSTALL@

@LOCAL_ZIP_FILES_UNINSTALL@

@PYTHON_MODULES_FILES_UNINSTALL@

  RMDIR /r "$INSTDIR\tcl"
  RMDIR /r "$INSTDIR\tk"

  RMDir $INSTDIR\doc\api
  RMDir $INSTDIR\doc
  RMDir $INSTDIR
  
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $StartMenuFolder
  
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons User's Guide (HTML) .lnk"
  
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons Manual Page (HTML) .lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons-Time Manual Page (HTML) .lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\SConsign Manual Page (HTML) .lnk"
  
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons API Reference (HTML) .lnk"
  
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons User's Guide (PDF) .lnk"
  
  Delete "$SMPROGRAMS\$StartMenuFolder\SCons API Reference (PDF) .lnk"
  
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  !insertmacro MultiUserDeleteRegKey "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PRODUCT_NAME@"
  
  !insertmacro MultiUserDeleteRegKey "Software\Classes\*\shell\scons_frontend"
  !insertmacro MultiUserDeleteRegKey "Software\Classes\Directory\shell\scons_frontend"

SectionEnd

Section "un.$(SEC_UnRemoveFromPath)" UnRemoveFromPath

  ${un.EnvVarUpdate} $0 "PATH" "R" $RegHive $INSTDIR

SectionEnd

Section /o "un.$(SEC_UnRemoveFromRegistry)" UnRemoveFromRegistry

    DeleteRegKey HKCU "@INSTALL_DIR_REG_KEY@"
    !insertmacro MultiUserDeleteRegKey "@INSTALL_DIR_REG_KEY@"

SectionEnd

Section /o "un.$(SEC_UnRemovePythonModules)" UnRemovePythonModules
SectionEnd

##########################################################################
# Uninstaller l10n
##########################################################################
LangString DESC_Uninstall  ${LANG_ENGLISH} "Uninstall SCons."
LangString DESC_Uninstall  ${LANG_GERMAN} "SCons deinstallieren."

LangString DESC_UnRemoveFromPath  ${LANG_ENGLISH} "Remove the SCons directory from path."
LangString DESC_UnRemoveFromPath  ${LANG_GERMAN} "Entfernt das Installationsverzeichnis aus dem Pfad."

LangString DESC_UnRemoveFromRegistry  ${LANG_ENGLISH} "Remove all SCons registry entries."
LangString DESC_UnRemoveFromRegistry  ${LANG_GERMAN} "Entfernt alle SCons-Registry-Einträge."

LangString DESC_UnRemovePythonModules  ${LANG_ENGLISH} "To remove the Python modules, use the Windows Control Panel."
LangString DESC_UnRemovePythonModules  ${LANG_GERMAN} "Verwenden sie die Systemsteuerung, um die Python-Module zu entfernen."

!insertmacro MUI_UNFUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${Uninstall}  $(DESC_Uninstall)
  !insertmacro MUI_DESCRIPTION_TEXT ${UnRemoveFromPath}  $(DESC_UnRemoveFromPath)
  !insertmacro MUI_DESCRIPTION_TEXT ${UnRemoveFromRegistry}  $(DESC_UnRemoveFromRegistry)
  !insertmacro MUI_DESCRIPTION_TEXT ${UnRemovePythonModules}  $(DESC_UnRemovePythonModules)
!insertmacro MUI_UNFUNCTION_DESCRIPTION_END

LangString UninstallMode ${LANG_ENGLISH} "Perform system-wide uninstall?"
LangString UninstallMode ${LANG_GERMAN} "Systemweite Deinstallation durchführen?"

##########################################################################
# Uninstaller functions
##########################################################################
Function un.onInit

  !insertmacro MUI_UNGETLANGUAGE
  !insertmacro MULTIUSER_UNINIT

  SectionSetFlags ${UnRemovePythonModules} ${SF_RO}
  
  ${if} $MultiUser.InstallMode == "AllUsers"
    MessageBox MB_YESNO $(UninstallMode) IDYES skip IDNO switchmode
    switchmode:
      Call un.MultiUser.InstallMode.CurrentUser
    skip:
  ${endif}
  
FunctionEnd
